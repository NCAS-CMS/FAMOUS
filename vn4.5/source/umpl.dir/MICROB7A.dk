*IF DEF,A03_7A                                                             MICROB7A.2     
C *****************************COPYRIGHT******************************     MICROB7A.3     
C (c) CROWN COPYRIGHT 1997, METEOROLOGICAL OFFICE, All Rights Reserved.    MICROB7A.4     
C                                                                          MICROB7A.5     
C Use, duplication or disclosure of this code is subject to the            MICROB7A.6     
C restrictions as set forth in the contract.                               MICROB7A.7     
C                                                                          MICROB7A.8     
C                Meteorological Office                                     MICROB7A.9     
C                London Road                                               MICROB7A.10    
C                BRACKNELL                                                 MICROB7A.11    
C                Berkshire UK                                              MICROB7A.12    
C                RG12 2SZ                                                  MICROB7A.13    
C                                                                          MICROB7A.14    
C If no contract has been raised with this copy of the code, the use,      MICROB7A.15    
C duplication or disclosure of it is strictly prohibited.  Permission      MICROB7A.16    
C to do so must first be obtained in writing from the Head of Numerical    MICROB7A.17    
C Modelling at the above address.                                          MICROB7A.18    
C ******************************COPYRIGHT******************************    MICROB7A.19    
!***********************************************************************   MICROB7A.20    
! Calculates the soil respiration based on a simplified version of the     MICROB7A.21    
! model of Raich et al. (1991).                                            MICROB7A.22    
!***********************************************************************   MICROB7A.23    
      SUBROUTINE MICROBE (LAND_FIELD,LAND_PTS,LAND1                        MICROB7A.24    
     &,                   CS,STH_SOIL,V_SAT,V_WILT,TSOIL,RESP_S)           MICROB7A.25    
                                                                           MICROB7A.26    
      IMPLICIT NONE                                                        MICROB7A.27    
                                                                           MICROB7A.28    
      INTEGER                                                              MICROB7A.29    
     & LAND_FIELD                 ! IN Total number of land points.        MICROB7A.30    
     &,LAND_PTS                   ! IN Number of land points to be         MICROB7A.31    
!                                 !    processed.                          MICROB7A.32    
     &,LAND1                      ! IN First land point to be              MICROB7A.33    
!                                 !    processed.                          MICROB7A.34    
                                                                           MICROB7A.35    
      REAL                                                                 MICROB7A.36    
     & CS(LAND_FIELD)             ! IN Soil carbon (kg C/m2).              MICROB7A.37    
     &,STH_SOIL(LAND_FIELD)       ! IN Top layer soil moisture as a        MICROB7A.38    
!                                 !    fraction of saturation (m3/m3).     MICROB7A.39    
     &,V_SAT(LAND_FIELD)          ! IN Volumetric soil moisture            MICROB7A.40    
!                                 !    concentration at saturation         MICROB7A.41    
!                                 !    (m3 H2O/m3 soil).                   MICROB7A.42    
     &,V_WILT(LAND_FIELD)         ! IN Volumetric soil moisture            MICROB7A.43    
!                                 !    concentration below which           MICROB7A.44    
!                                 !    stomata close (m3 H2O/m3 soil).     MICROB7A.45    
                                  !    as a fraction of saturation.        MICROB7A.46    
     &,TSOIL(LAND_FIELD)          ! IN Soil temperature (K).               MICROB7A.47    
     &,RESP_S(LAND_FIELD)         ! OUT Soil respiration (kg C/m2/s).      MICROB7A.48    
     &,FSTH,FTEMP                 ! WORK Factors describing the            MICROB7A.49    
!                                 !      influence of soil moisture and    MICROB7A.50    
!                                 !      soil temperature respectively     MICROB7A.51    
!                                 !      on the soil respiration.          MICROB7A.52    
     &,STH_OPT                    ! WORK Fractional soil moisture at       MICROB7A.53    
!                                 !      which respiration is maximum.     MICROB7A.54    
     &,STH_WILT                   ! WORK Wilting soil moisture as a        MICROB7A.55    
!                                 !      fraction of saturation.           MICROB7A.56    
      INTEGER                                                              MICROB7A.57    
     & L                          ! Loop counter                           MICROB7A.58    
                                                                           MICROB7A.59    
!-----------------------------------------------------------------------   MICROB7A.60    
! Local parameters                                                         MICROB7A.61    
!-----------------------------------------------------------------------   MICROB7A.62    
      REAL                                                                 MICROB7A.63    
     & KAPS                       ! Specific soil respiration rate         MICROB7A.64    
!                                 ! at 25 deg ! and optimum soil           MICROB7A.65    
!                                 ! moisture (/s).                         MICROB7A.66    
     &,Q10                        ! Q10 factor for soil respiration.       MICROB7A.67    
      PARAMETER (KAPS = 0.5E-8, Q10 = 2.0)                                 MICROB7A.68    
                                                                           MICROB7A.69    
                                                                           MICROB7A.70    
      DO L=LAND1,LAND1+LAND_PTS-1                                          MICROB7A.71    
                                                                           MICROB7A.72    
        IF (V_SAT(L) .GT. 0.0) THEN                                        MICROB7A.73    
                                                                           MICROB7A.74    
          STH_WILT = V_WILT(L) / V_SAT(L)                                  MICROB7A.75    
          STH_OPT = 0.5 * (1 + STH_WILT)                                   MICROB7A.76    
                                                                           MICROB7A.77    
          IF (STH_SOIL(L) .LE. STH_WILT) THEN                              MICROB7A.78    
            FSTH = 0.2                                                     MICROB7A.79    
          ELSEIF (STH_SOIL(L) .GT. STH_WILT .AND.                          MICROB7A.80    
     &            STH_SOIL(L) .LE. STH_OPT) THEN                           MICROB7A.81    
            FSTH = 0.2 + 0.8 * ((STH_SOIL(L) - STH_WILT)                   MICROB7A.82    
     &                        / (STH_OPT - STH_WILT))                      MICROB7A.83    
          ELSEIF (STH_SOIL(L) .GT. STH_OPT) THEN                           MICROB7A.84    
            FSTH = 1 - 0.8 * (STH_SOIL(L) - STH_OPT)                       MICROB7A.85    
          ENDIF                                                            MICROB7A.86    
                                                                           MICROB7A.87    
          FTEMP = Q10 ** (0.1 * (TSOIL(L) - 298.15))                       MICROB7A.88    
                                                                           MICROB7A.89    
          RESP_S(L) = KAPS * CS(L) * FSTH * FTEMP                          MICROB7A.90    
                                                                           MICROB7A.91    
        ELSE                                                               MICROB7A.92    
                                                                           MICROB7A.93    
          RESP_S(L) = 0.0                                                  MICROB7A.94    
                                                                           MICROB7A.95    
        ENDIF                                                              MICROB7A.96    
                                                                           MICROB7A.97    
      ENDDO                                                                MICROB7A.98    
                                                                           MICROB7A.99    
      RETURN                                                               MICROB7A.100   
                                                                           MICROB7A.101   
      END                                                                  MICROB7A.102   
*ENDIF                                                                     MICROB7A.103   
