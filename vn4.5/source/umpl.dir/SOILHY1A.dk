*IF DEF,A08_1A                                                             AJS1F401.1527  
C ******************************COPYRIGHT******************************    GTS2F400.9253  
C (c) CROWN COPYRIGHT 1995, METEOROLOGICAL OFFICE, All Rights Reserved.    GTS2F400.9254  
C                                                                          GTS2F400.9255  
C Use, duplication or disclosure of this code is subject to the            GTS2F400.9256  
C restrictions as set forth in the contract.                               GTS2F400.9257  
C                                                                          GTS2F400.9258  
C                Meteorological Office                                     GTS2F400.9259  
C                London Road                                               GTS2F400.9260  
C                BRACKNELL                                                 GTS2F400.9261  
C                Berkshire UK                                              GTS2F400.9262  
C                RG12 2SZ                                                  GTS2F400.9263  
C                                                                          GTS2F400.9264  
C If no contract has been raised with this copy of the code, the use,      GTS2F400.9265  
C duplication or disclosure of it is strictly prohibited.  Permission      GTS2F400.9266  
C to do so must first be obtained in writing from the Head of Numerical    GTS2F400.9267  
C Modelling at the above address.                                          GTS2F400.9268  
C ******************************COPYRIGHT******************************    GTS2F400.9269  
C                                                                          GTS2F400.9270  
C*LL  SUBROUTINE SOIL_HYD ----------------------------------------------   SOILHY1A.3     
CLL                                                                        SOILHY1A.4     
CLL  Purpose:  Firstly, the soil moisture content (SMC) is incremented     SOILHY1A.5     
CLL            by the infiltration amount (FW) calculated at P252 and      SOILHY1A.6     
CLL            decremented by the evaporation (E_SOIL) calculated at       SOILHY1A.7     
CLL            P24.  Secondly, the sub-surface ("slow") runoff of soil     SOILHY1A.8     
CLL            moisture is calculated, and the SMC decreased               SOILHY1A.9     
CLL            appropriately.                                              SOILHY1A.10    
CLL                                                                        SOILHY1A.11    
CLL  Model            Modification history from model version 3.0:         SOILHY1A.12    
CLL version  Date                                                          SOILHY1A.13    
CLL   4.5    Jul. 98  Kill the IBM specific lines (JCThil)                 AJC1F405.18    
CLL                                                                        SOILHY1A.14    
CLL  Programming standard: Unified Model Documentation Paper No.4          SOILHY1A.15    
CLL                        version no. 2, dated 18/1/90.                   SOILHY1A.16    
CLL                                                                        SOILHY1A.17    
CLL  Logical component covered: P253.                                      SOILHY1A.18    
CLL                                                                        SOILHY1A.19    
CLL  System task:                                                          SOILHY1A.20    
CLL                                                                        SOILHY1A.21    
CLL  Documentation: UM Documentation Paper No 25.                          SOILHY1A.22    
CLLEND------------------------------------------------------------------   SOILHY1A.23    
C*                                                                         SOILHY1A.24    
C*L  ARGUMENTS:---------------------------------------------------------   SOILHY1A.25    
      SUBROUTINE SOIL_HYD (                                                SOILHY1A.26    
     + ROOT_DEPTH,BS,C_EAG,E_SOIL,FW,TIMESTEP,POINTS,SMC                   SOILHY1A.27    
     +,SLOW_RUNOFF,STF_SLOW_RUNOFF                                         SOILHY1A.28    
     +)                                                                    SOILHY1A.29    
      IMPLICIT NONE                                                        SOILHY1A.30    
      INTEGER POINTS                                                       SOILHY1A.31    
      REAL                                                                 SOILHY1A.35    
     + E_SOIL(POINTS)     ! IN Evaporation from soil surface (+ve          SOILHY1A.36    
C                         !    upwards) (kg per sq m per sec).             SOILHY1A.37    
     +,FW(POINTS)         ! IN Throughfall from canopy plus snowmelt       SOILHY1A.38    
C                         !    minus surface runoff (kg per sq m per s).   SOILHY1A.39    
     +,ROOT_DEPTH(POINTS) ! IN Root depth (m).                             SOILHY1A.40    
     +,BS(POINTS)         ! IN Coefficient in conductivity calculation,    SOILHY1A.41    
C                         !    defined by eq P253.4 (kg per sq m per s).   SOILHY1A.42    
C                         !    This is BS in the documentation.            SOILHY1A.43    
     +,C_EAG(POINTS)      ! IN Exponent in conductivity calculation.       SOILHY1A.44    
C                         !    This is C in the documentation.             SOILHY1A.45    
      REAL                                                                 SOILHY1A.46    
     + TIMESTEP           ! IN Timestep in seconds.                        SOILHY1A.47    
      REAL                                                                 SOILHY1A.48    
     + SMC(POINTS)        ! INOUT Soil moisture content (kg per sq m).     SOILHY1A.49    
      REAL                                                                 SOILHY1A.50    
     + SLOW_RUNOFF(POINTS) ! OUT "Slow" runoff (kg per sq m per sec).      SOILHY1A.51    
      LOGICAL STF_SLOW_RUNOFF ! IN stash flag for slow runoff              SOILHY1A.52    
C-----------------------------------------------------------------------   SOILHY1A.53    
C   workspace required.                                                    SOILHY1A.54    
C   one array required.                                                    SOILHY1A.55    
C  No EXTERNAL subprograms are called.                                     SOILHY1A.56    
C-----------------------------------------------------------------------   SOILHY1A.57    
      REAL SRO_DT(POINTS)  ! Slow runoff (kg per sq m per timestep).       SOILHY1A.61    
C*                                                                         SOILHY1A.63    
C  Model constants                                                         SOILHY1A.64    
*CALL C_RHOWAT                                                             SOILHY1A.65    
C                                                                          SOILHY1A.66    
C-----------------------------------------------------------------------   SOILHY1A.67    
C  Define local variables.                                                 SOILHY1A.68    
C-----------------------------------------------------------------------   SOILHY1A.69    
      INTEGER I            ! Loop counter (horizontal field index).        SOILHY1A.70    
C-----------------------------------------------------------------------   SOILHY1A.71    
CL     No significant structure, apart from loop round points :-           SOILHY1A.72    
C-----------------------------------------------------------------------   SOILHY1A.73    
      DO 1 I=1,POINTS                                                      SOILHY1A.74    
C-----------------------------------------------------------------------   SOILHY1A.75    
CL  1. Modify SMC as a result of infiltration and evaporation.  P253.1.    SOILHY1A.76    
C-----------------------------------------------------------------------   SOILHY1A.77    
        SMC(I) = SMC(I) + TIMESTEP*(FW(I) - E_SOIL(I))                     SOILHY1A.78    
        SRO_DT(I) = 0.0                                                    SOILHY1A.79    
        IF(SMC(I).GT.0.0 .AND. ROOT_DEPTH(I).GT.0.0)THEN                   SOILHY1A.80    
C-----------------------------------------------------------------------   SOILHY1A.81    
CL  2. Calculate subsurface runoff integrated over timestep, in kg per     SOILHY1A.82    
CL     sq metre (SRO_DT), eqn P253.6 in different units.  This is          SOILHY1A.83    
CL     equivalent to calculating the actual hydraulic conductivity of      SOILHY1A.84    
CL     the soil as a function of SMC (see DCTN 38, eqn 3.10).              SOILHY1A.85    
CL     Then update the SMC (eqn P253.7).                                   SOILHY1A.86    
C-----------------------------------------------------------------------   SOILHY1A.87    
          SRO_DT(I) = BS(I)                                                SOILHY1A.88    
     +            * (( SMC(I) /(RHO_WATER*ROOT_DEPTH(I)))**C_EAG(I))       SOILHY1A.89    
     +            * TIMESTEP                                               SOILHY1A.90    
          IF (SRO_DT(I).GT.SMC(I)) SRO_DT(I)=SMC(I)                        SOILHY1A.91    
          SMC(I) = SMC(I) - SRO_DT(I)                                      SOILHY1A.92    
        ENDIF                                                              SOILHY1A.93    
    1 CONTINUE                                                             SOILHY1A.94    
C-----------------------------------------------------------------------   SOILHY1A.95    
CL  3. Convert runoff to kg per sq metre per second for diagnostic.        SOILHY1A.96    
C-----------------------------------------------------------------------   SOILHY1A.97    
      IF (STF_SLOW_RUNOFF) THEN                                            SOILHY1A.98    
        DO I=1,POINTS                                                      SOILHY1A.99    
          SLOW_RUNOFF(I) = SRO_DT(I) / TIMESTEP                            SOILHY1A.100   
        ENDDO                                                              SOILHY1A.101   
      ENDIF                                                                SOILHY1A.102   
      RETURN                                                               SOILHY1A.103   
      END                                                                  SOILHY1A.104   
*ENDIF                                                                     SOILHY1A.105   
