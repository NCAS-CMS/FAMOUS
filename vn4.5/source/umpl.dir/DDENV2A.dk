*IF DEF,A05_2A,OR,DEF,A05_2C                                               AJX1F405.154   
C ******************************COPYRIGHT******************************    GTS2F400.1729  
C (c) CROWN COPYRIGHT 1995, METEOROLOGICAL OFFICE, All Rights Reserved.    GTS2F400.1730  
C                                                                          GTS2F400.1731  
C Use, duplication or disclosure of this code is subject to the            GTS2F400.1732  
C restrictions as set forth in the contract.                               GTS2F400.1733  
C                                                                          GTS2F400.1734  
C                Meteorological Office                                     GTS2F400.1735  
C                London Road                                               GTS2F400.1736  
C                BRACKNELL                                                 GTS2F400.1737  
C                Berkshire UK                                              GTS2F400.1738  
C                RG12 2SZ                                                  GTS2F400.1739  
C                                                                          GTS2F400.1740  
C If no contract has been raised with this copy of the code, the use,      GTS2F400.1741  
C duplication or disclosure of it is strictly prohibited.  Permission      GTS2F400.1742  
C to do so must first be obtained in writing from the Head of Numerical    GTS2F400.1743  
C Modelling at the above address.                                          GTS2F400.1744  
C ******************************COPYRIGHT******************************    GTS2F400.1745  
C                                                                          GTS2F400.1746  
CLL  SUBROUTINE DD_ENV-------------------------------------------------    DDENV2A.3     
CLL                                                                        DDENV2A.4     
CLL  PURPOSE : CALCULATE THE EFFECT OF THE DOWNDRAUGHT                     DDENV2A.5     
CLL            ON THE LARGE_SCALE ATMOSPHERE                               DDENV2A.6     
CLL                                                                        DDENV2A.7     
CLL  SUITABLE FOR SINGLE COLUMN MODEL USE                                  DDENV2A.8     
CLL                                                                        DDENV2A.9     
CLL  CODE WRITTEN FOR CRAY Y-MP BY S.BETT AND D.GREGORY AUTUMN 1991        DDENV2A.10    
CLL                                                                        DDENV2A.11    
CLL  MODEL            MODIFICATION HISTORY FROM MODEL VERSION 3.0:         DDENV2A.12    
CLL VERSION  DATE                                                          DDENV2A.13    
CLL                                                                        DDENV2A.14    
CLL  PROGRAMMING STANDARDS : UNIFIED MODEL DOCUMENTATION PAPER NO. 3       DDENV2A.15    
CLL  VERSION NO. 4  DATED 5/2/92                                           DDENV2A.16    
CLL                                                                        DDENV2A.17    
CLL  LOGICAL COMPONENTS COVERED:                                           DDENV2A.18    
CLL                                                                        DDENV2A.19    
CLL  SYSTEM TASK : P27                                                     DDENV2A.20    
CLL                                                                        DDENV2A.21    
CLL  DOCUMENTATION : UNIFIED MODEL DOCUMENTATION PAPER P27                 DDENV2A.22    
CLL                                                                        DDENV2A.23    
CLLEND-----------------------------------------------------------------    DDENV2A.24    
C                                                                          DDENV2A.25    
C*L  ARGUMENTS---------------------------------------------------------    DDENV2A.26    
C                                                                          DDENV2A.27    
      SUBROUTINE DD_ENV (NPNTS,THDD_K,THDD_KM1,QDD_K,QDD_KM1,THE_K,        DDENV2A.28    
     *                   THE_KM1,QE_K,QE_KM1,DTHBYDT_K,DTHBYDT_KM1,        DDENV2A.29    
     *                   DQBYDT_K,DQBYDT_KM1,FLX_DD_K,FLX_DD_KM1,DELPK,    DDENV2A.30    
     *                   DELPKM1,DELTD,DELQD,AMDETK,EKM14,                 DDENV2A.31    
     *                   B_DD_END,BDD_START,BDD_ON)                        DDENV2A.32    
C                                                                          DDENV2A.33    
      IMPLICIT NONE                                                        DDENV2A.34    
C                                                                          DDENV2A.35    
C-----------------------------------------------------------------------   DDENV2A.36    
C VECTOR LENGTHS AND LOOP COUNTERS                                         DDENV2A.37    
C-----------------------------------------------------------------------   DDENV2A.38    
C                                                                          DDENV2A.39    
      INTEGER NPNTS                 ! IN VECTOR LENGTH                     DDENV2A.40    
C                                                                          DDENV2A.41    
      INTEGER I                     ! LOOP COUNTER                         DDENV2A.42    
C                                                                          DDENV2A.43    
C-----------------------------------------------------------------------   DDENV2A.44    
C VARIABLES WHICH ARE INPUT                                                DDENV2A.45    
C-----------------------------------------------------------------------   DDENV2A.46    
C                                                                          DDENV2A.47    
      REAL THDD_K(NPNTS)            ! IN DOWNDRAUGHT POTENTIAL             DDENV2A.48    
                                    !    TEMPERATURE IN LAYER K (K)        DDENV2A.49    
C                                                                          DDENV2A.50    
      REAL THDD_KM1(NPNTS)          ! IN DOWNDRAUGHT POTENTIAL             DDENV2A.51    
                                    !    TEMPERATURE IN LAYER K-1 (K)      DDENV2A.52    
C                                                                          DDENV2A.53    
      REAL QDD_K(NPNTS)             ! IN DOWNDRAUGHT MIXING RATIO          DDENV2A.54    
                                    !    AT LAYER K (KG/KG)                DDENV2A.55    
C                                                                          DDENV2A.56    
      REAL QDD_KM1(NPNTS)           ! IN DOWNDRAUGHT MIXING RATIO          DDENV2A.57    
                                    !    AT LAYER K-1 (KG/KG)              DDENV2A.58    
C                                                                          DDENV2A.59    
      REAL THE_K(NPNTS)             ! IN POTENTIAL TEMPERATURE OF          DDENV2A.60    
                                    !    ENVIRONMENT IN LAYER K (K)        DDENV2A.61    
C                                                                          DDENV2A.62    
      REAL THE_KM1(NPNTS)           ! IN POTENTIAL TEMPERATURE OF          DDENV2A.63    
                                    !    ENVIRONMENT IN LAYER K-1 (K)      DDENV2A.64    
C                                                                          DDENV2A.65    
      REAL QE_K(NPNTS)              ! IN MIXING RATIO AT LAYER K (KG/KG)   DDENV2A.66    
C                                                                          DDENV2A.67    
      REAL QE_KM1(NPNTS)            ! IN MIXING RATIO AT LAYER K-1         DDENV2A.68    
                                    !    (KG/KG)                           DDENV2A.69    
C                                                                          DDENV2A.70    
      REAL FLX_DD_K(NPNTS)          ! IN MASS FLUX IN LAYER K (PA/S)       DDENV2A.71    
C                                                                          DDENV2A.72    
      REAL FLX_DD_KM1(NPNTS)        ! IN MASS FLUX IN LAYER K-1 (PA/S)     DDENV2A.73    
C                                                                          DDENV2A.74    
      REAL DELPK(NPNTS)             ! IN DIFFERENCE IN PRESSURE ACROSS     DDENV2A.75    
                                    !    LAYER K (PA)                      DDENV2A.76    
C                                                                          DDENV2A.77    
      REAL DELPKM1(NPNTS)           ! IN DIFFERENCE IN PRESSURE ACROSS     DDENV2A.78    
                                    !    LAYER K-1 (PA)                    DDENV2A.79    
C                                                                          DDENV2A.80    
      REAL DELTD(NPNTS)             ! IN COOLING NECESSARY TO ACHIEVE      DDENV2A.81    
                                    !    SATURATION (K)                    DDENV2A.82    
C                                                                          DDENV2A.83    
      REAL DELQD(NPNTS)             ! IN MOISTENING NECESSARY TO ACHIEVE   DDENV2A.84    
                                    !    SATURATION (KG/KG)                DDENV2A.85    
C                                                                          DDENV2A.86    
      REAL AMDETK(NPNTS)            ! IN MIXING DETRAINMENT AT LEVEL K     DDENV2A.87    
                                    !    MULTIPLIED BY APPROPRIATE LAYER   DDENV2A.88    
                                    !    THICKNESS                         DDENV2A.89    
C                                                                          DDENV2A.90    
      REAL EKM14(NPNTS)             ! IN EXNER RATIO AT LAYER K-1/4        DDENV2A.91    
C                                                                          DDENV2A.92    
      LOGICAL B_DD_END(NPNTS)       ! IN MASK FOR THOSE POINTS WHERE       DDENV2A.93    
                                    !    DOWNDRAUGHT IS TERMINATING        DDENV2A.94    
C                                                                          DDENV2A.95    
      LOGICAL BDD_START(NPNTS)      ! IN MASK FOR THOSE POINTS WHERE       DDENV2A.96    
                                    !    DOWNDRAUGHT IS STARTING           DDENV2A.97    
C                                                                          DDENV2A.98    
      LOGICAL BDD_ON(NPNTS)         ! IN MASK FOR THOSE POINTS WHERE       DDENV2A.99    
                                    !    DOWNDRAUGHT IS ON                 DDENV2A.100   
C                                                                          DDENV2A.101   
C-----------------------------------------------------------------------   DDENV2A.102   
C VARIABLES WHICH ARE INPUT AND OUTPUT                                     DDENV2A.103   
C-----------------------------------------------------------------------   DDENV2A.104   
C                                                                          DDENV2A.105   
      REAL DTHBYDT_K(NPNTS)         ! INOUT                                DDENV2A.106   
                                    ! IN  INCREMENT TO POTENTIAL           DDENV2A.107   
                                    !     TEMPERATURE IN LAYER K (K/S)     DDENV2A.108   
                                    ! OUT UPDATED INCREMENT TO POTENTIAL   DDENV2A.109   
                                    !     TEMPERATURE IN LAYER K (K/S)     DDENV2A.110   
C                                                                          DDENV2A.111   
      REAL DTHBYDT_KM1(NPNTS)       ! INOUT                                DDENV2A.112   
                                    ! IN  INCREMENT TO POTENTIAL           DDENV2A.113   
                                    !     TEMPERATURE AT LAYER K-1 (K/S)   DDENV2A.114   
                                    ! OUT UPDATED INCREMENT TO POTENTIAL   DDENV2A.115   
                                    !     TEMPERATURE AT LAYER K-1 (K/S)   DDENV2A.116   
C                                                                          DDENV2A.117   
      REAL DQBYDT_KM1(NPNTS)        ! INOUT                                DDENV2A.118   
                                    ! IN  INCREMENT TO MIXING RATIO AT     DDENV2A.119   
                                    !     LAYER K-1 (KG/KG/S)              DDENV2A.120   
                                    ! OUT UPDATED INCREMENT TO MIXING      DDENV2A.121   
                                    !     RATIO AT LAYER K-1 (KG/KG/S)     DDENV2A.122   
C                                                                          DDENV2A.123   
      REAL DQBYDT_K(NPNTS)          ! INOUT                                DDENV2A.124   
                                    ! IN  INCREMENT TO MIXING RATIO        DDENV2A.125   
                                    !     AT LAYER K (KG/KG/S)             DDENV2A.126   
                                    ! OUT UPDATED INCREMENT TO MIXING      DDENV2A.127   
                                    !     RATIO AT LAYER K (KG/KG/S)       DDENV2A.128   
C                                                                          DDENV2A.129   
C-----------------------------------------------------------------------   DDENV2A.130   
C VARIABLES WHICH ARE DEFINED LOCALLY                                      DDENV2A.131   
C-----------------------------------------------------------------------   DDENV2A.132   
C                                                                          DDENV2A.133   
      REAL TEMPRY                   ! USED IN CALCULATIONS OF THE          DDENV2A.134   
                                    ! EFFECT ON THE ENVIRONMENT            DDENV2A.135   
C                                                                          DDENV2A.136   
C-----------------------------------------------------------------------   DDENV2A.137   
C CALCULATE THE EFFECT ON THE ENVIRONMENT IN LAYER K                       DDENV2A.138   
C-----------------------------------------------------------------------   DDENV2A.139   
C                                                                          DDENV2A.140   
      DO I=1,NPNTS                                                         DDENV2A.141   
       IF (BDD_ON(I)) THEN                                                 DDENV2A.142   
C                                                                          DDENV2A.143   
C-----------------------------------------------------------------------   DDENV2A.144   
C SUBTRACT ENERGY USED TO FORM DOWNDRAUGHT                                 DDENV2A.145   
C-----------------------------------------------------------------------   DDENV2A.146   
C                                                                          DDENV2A.147   
       TEMPRY = FLX_DD_K(I)/DELPK(I)                                       DDENV2A.148   
       IF (BDD_START(I)) THEN                                              DDENV2A.149   
         DTHBYDT_K(I) = DTHBYDT_K(I)-TEMPRY*DELTD(I)                       DDENV2A.150   
         DQBYDT_K(I) = DQBYDT_K(I)-TEMPRY*DELQD(I)                         DDENV2A.151   
       END IF                                                              DDENV2A.152   
C                                                                          DDENV2A.153   
C-----------------------------------------------------------------------   DDENV2A.154   
C EFFECT OF CONVECTION AND DOWNDRAUGHT UPON POTENTIAL TEMPERATURE OF       DDENV2A.155   
C LAYER K                                                                  DDENV2A.156   
C-----------------------------------------------------------------------   DDENV2A.157   
C                                                                          DDENV2A.158   
       DTHBYDT_K(I) = DTHBYDT_K(I) + TEMPRY * (                            DDENV2A.159   
     *                                                                     DDENV2A.160   
     *          (1.0+EKM14(I)) * (1.0-AMDETK(I)) *      ! COMPENSATING     DDENV2A.161   
     *           (THE_KM1(I)-THE_K(I))                  ! SUBSIDENCE       DDENV2A.162   
     *        +                                                            DDENV2A.163   
     *          AMDETK(I)* (THDD_K(I)-THE_K(I))         ! MIXING           DDENV2A.164   
     *        )                                         ! DETRAINMENT      DDENV2A.165   
C                                                                          DDENV2A.166   
C-----------------------------------------------------------------------   DDENV2A.167   
C EFFECT OF CONVECTION AND DOWNDRAUGHT UPON MIXING RATIO OF                DDENV2A.168   
C LAYER K                                                                  DDENV2A.169   
C-----------------------------------------------------------------------   DDENV2A.170   
C                                                                          DDENV2A.171   
       DQBYDT_K(I) = DQBYDT_K(I) + TEMPRY * (                              DDENV2A.172   
     *                                                                     DDENV2A.173   
     *      (1.0+EKM14(I)) * (1.0-AMDETK(I)) *       ! COMPENSATING        DDENV2A.174   
     *      (QE_KM1(I)-QE_K(I))                      ! SUBSIDENCE          DDENV2A.175   
     *    +                                                                DDENV2A.176   
     *      AMDETK(I)* (QDD_K(I)-QE_K(I))            ! MIXING              DDENV2A.177   
     *    )                                          ! DETRAINMENT         DDENV2A.178   
C                                                                          DDENV2A.179   
C-----------------------------------------------------------------------   DDENV2A.180   
C TERMINAL DETRAINMENT AND SUBSIDENCE IN TERMINAL LAYER                    DDENV2A.181   
C-----------------------------------------------------------------------   DDENV2A.182   
C                                                                          DDENV2A.183   
         IF (B_DD_END(I)) THEN                                             DDENV2A.184   
           TEMPRY = FLX_DD_KM1(I)/DELPKM1(I)                               DDENV2A.185   
           DTHBYDT_KM1(I) = DTHBYDT_KM1(I)+TEMPRY*                         DDENV2A.186   
     *                      (THDD_KM1(I)-THE_KM1(I))                       DDENV2A.187   
           DQBYDT_KM1(I) = DQBYDT_KM1(I)+TEMPRY*(QDD_KM1(I)-QE_KM1(I))     DDENV2A.188   
         END IF                                                            DDENV2A.189   
C                                                                          DDENV2A.190   
       END IF                                                              DDENV2A.191   
      END DO                                                               DDENV2A.192   
C                                                                          DDENV2A.193   
      RETURN                                                               DDENV2A.194   
      END                                                                  DDENV2A.195   
C                                                                          DDENV2A.196   
*ENDIF                                                                     DDENV2A.197   
