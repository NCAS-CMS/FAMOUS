*IF DEF,OCEAN                                                              @DYALLOC.4438  
C ******************************COPYRIGHT******************************    GTS2F400.10801 
C (c) CROWN COPYRIGHT 1995, METEOROLOGICAL OFFICE, All Rights Reserved.    GTS2F400.10802 
C                                                                          GTS2F400.10803 
C Use, duplication or disclosure of this code is subject to the            GTS2F400.10804 
C restrictions as set forth in the contract.                               GTS2F400.10805 
C                                                                          GTS2F400.10806 
C                Meteorological Office                                     GTS2F400.10807 
C                London Road                                               GTS2F400.10808 
C                BRACKNELL                                                 GTS2F400.10809 
C                Berkshire UK                                              GTS2F400.10810 
C                RG12 2SZ                                                  GTS2F400.10811 
C                                                                          GTS2F400.10812 
C If no contract has been raised with this copy of the code, the use,      GTS2F400.10813 
C duplication or disclosure of it is strictly prohibited.  Permission      GTS2F400.10814 
C to do so must first be obtained in writing from the Head of Numerical    GTS2F400.10815 
C Modelling at the above address.                                          GTS2F400.10816 
C ******************************COPYRIGHT******************************    GTS2F400.10817 
C                                                                          GTS2F400.10818 
CLL=============== SUBROUTINE UNESCO ===================================   UNESCO.2     
CLL   THIS SUBROUTINE CALCULATES THE DENSITY OF SEAWATER USING THE         UNESCO.3     
CLL   STANDARD EQUATION OF STATE RECOMMENDED BY UNESCO(1981).              UNESCO.4     
CLL                                                                        UNESCO.5     
CLL RECODED FROM THE GFDL MODULAR OCEAN MODEL FOR USE IN THE               UNESCO.6     
CLL UNIFIED MODEL BY :      OSCAR  ALVES   22/2/93                         UNESCO.7     
CLL REVIEWED BY:                                                           UNESCO.8     
CLL                                                                        UNESCO.9     
CLL   INPUT 1UNITS :                                                       UNESCO.10    
CLL     IN-SITU TEMPERATURE (T): 1EEGREES CENTIGRAEE                       UNESCO.11    
CLL     SALINITY (S): PRACTICAL SALINITY UNITS                             UNESCO.12    
CLL     PRESSURE (PIN): DECIBARS, APPROX. AS METERS OF EEPTH               UNESCO.13    
CLL   OUTPUT 1UNITS :                                                      UNESCO.14    
CLL     EENSITY(RHO): KILOGRAMS PER CUBIC METER                            UNESCO.15    
CLL                                                                        UNESCO.16    
CLL   REFERENCES:                                                          UNESCO.17    
CLL      GILL, A., ATMOSPHERE-OCEAN EYNAMICS: INTERNATIONAL GEOPHYSICAL    UNESCO.18    
CLL       SERIES NO. 30. ACAEEMIC PRESS, LONEON, 1982, PP 599-600.         UNESCO.19    
CLL      UNESCO, 10TH REPORT OF THE JOINT PANEL ON OCEANOGRAPHIC TABLES    UNESCO.20    
CLL        ANE STANEARES. UNESCO TECH. PAPERS IN MARINE SCI. NO. 36,       UNESCO.21    
CLL        PARIS, 1981.                                                    UNESCO.22    
CLL                                                                        UNESCO.23    
CLL   CHECK VALUES:                                                        UNESCO.24    
CLL   RHO=1059.82037676    - CRAY 64 BIT                                   UNESCO.25    
CLL      =1059.8203767598  - IEEE 64 BIT                                   UNESCO.26    
CLL   FOR T=40.0,S=40.0,PIN=10000.0                                        UNESCO.27    
CLL                                                                        UNESCO.28    
CLL---------------------------------------------------------------------   UNESCO.29    
CLLEND                                                                     UNESCO.30    
C*L                                                                        UNESCO.31    
      SUBROUTINE UNESCO (T, S, PIN, RHO)                                   UNESCO.32    
                                                                           UNESCO.33    
      IMPLICIT NONE                                                        UNESCO.34    
C-- DEFINE ARGUMENTS                                                       UNESCO.35    
      REAL                                                                 UNESCO.36    
     & T         ! IN TEMPERATURE (DEG C)                                  UNESCO.37    
     &,S         ! IN SALINITY (PSU)                                       UNESCO.38    
     &,PIN       ! IN PRESSURE  (DECIBARS)                                 UNESCO.39    
     &,RHO       ! OUT DENSITY (KG/M3)                                     UNESCO.40    
                                                                           UNESCO.41    
C-- DEFINE LOCAL VARIABLES                                                 UNESCO.42    
       REAL                                                                UNESCO.43    
     & C1P5     ! CONSTANT                                                 UNESCO.44    
     &,P        ! PRESSURE IN BARS                                         UNESCO.45    
     &,RW       ! TEMPORARY STORE                                          UNESCO.46    
     &,RSTO     ! TEMPORARY STORE                                          UNESCO.47    
     &,XKW      ! TEMPORARY STORE                                          UNESCO.48    
     &,XKSTO    ! TEMPORARY STORE                                          UNESCO.49    
     &,XKSTP    ! TEMPORARY STORE                                          UNESCO.50    
                                                                           UNESCO.51    
C                                                                          UNESCO.52    
C                                                                          UNESCO.53    
C                                                                          UNESCO.54    
C=======================================================================   UNESCO.55    
C                                                                          UNESCO.56    
      C1P5 = 1.5E0                                                         UNESCO.57    
C                                                                          UNESCO.58    
C  CONVERT FROM EEPTH 1M  (EECIBARS) TO BARS                               UNESCO.59    
      P = PIN * 1.0E-1                                                     UNESCO.60    
C                                                                          UNESCO.61    
      RW =     9.99842594E2 + 6.793952E-2*T - 9.095290E-3*T**2             UNESCO.62    
     &       + 1.001685E-4*T**3 - 1.120083E-6*T**4 + 6.536332E-9*T**5      UNESCO.63    
C                                                                          UNESCO.64    
      RSTO =   RW + (8.24493E-1 - 4.0899E-3*T + 7.6438E-5*T**2             UNESCO.65    
     &       - 8.2467E-7*T**3 + 5.3875E-9*T**4) * S                        UNESCO.66    
     &       + (-5.72466E-3 + 1.0227E-4*T - 1.6546E-6*T**2) * S**C1P5      UNESCO.67    
     &       + 4.8314E-4 * S**2                                            UNESCO.68    
C                                                                          UNESCO.69    
      XKW =     1.965221E4 + 1.484206E2*T - 2.327105E0*T**2 +              UNESCO.70    
     &          1.360477E-2*T**3 - 5.155288E-5*T**4                        UNESCO.71    
C                                                                          UNESCO.72    
      XKSTO =   XKW + (5.46746E1 - 6.03459E-1*T + 1.09987E-2*T**2          UNESCO.73    
     &         - 6.1670E-5*T**3) * S                                       UNESCO.74    
     &         + (7.944E-2 + 1.6483E-2*T - 5.3009E-4*T**2) * S**C1P5       UNESCO.75    
C                                                                          UNESCO.76    
      XKSTP =   XKSTO + (3.239908E0 + 1.43713E-3*T + 1.16092E-4*T**2       UNESCO.77    
     &       - 5.77905E-7*T**3) * P                                        UNESCO.78    
     &       + (2.2838E-3 - 1.0981E-5*T - 1.6078E-6*T**2) * P * S          UNESCO.79    
     &       + 1.91075E-4 * P * S**C1P5                                    UNESCO.80    
     &       + (8.50935E-5 - 6.12293E-6*T + 5.2787E-8*T**2) * P**2         UNESCO.81    
     &       + (-9.9348E-7 + 2.0816E-8*T + 9.1697E-10*T**2) * P**2 * S     UNESCO.82    
C                                                                          UNESCO.83    
      RHO =    RSTO / (1.0E0 - P/XKSTP)                                    UNESCO.84    
C                                                                          UNESCO.85    
      RETURN                                                               UNESCO.86    
      END                                                                  UNESCO.87    
*ENDIF                                                                     @DYALLOC.4439  
