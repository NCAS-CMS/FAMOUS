*IF DEF,A05_3B,OR,DEF,A05_3C                                               AJX1F405.168   
C (c) CROWN COPYRIGHT 1995, METEOROLOGICAL OFFICE, All Rights Reserved.    GTS2F400.14691 
C                                                                          GTS2F400.14692 
C Use, duplication or disclosure of this code is subject to the            GTS2F400.14693 
C restrictions as set forth in the contract.                               GTS2F400.14694 
C                                                                          GTS2F400.14695 
C                Meteorological Office                                     GTS2F400.14696 
C                London Road                                               GTS2F400.14697 
C                BRACKNELL                                                 GTS2F400.14698 
C                Berkshire UK                                              GTS2F400.14699 
C                RG12 2SZ                                                  GTS2F400.14700 
C                                                                          GTS2F400.14701 
C If no contract has been raised with this copy of the code, the use,      GTS2F400.14702 
C duplication or disclosure of it is strictly prohibited.  Permission      GTS2F400.14703 
C to do so must first be obtained in writing from the Head of Numerical    GTS2F400.14704 
C Modelling at the above address.                                          GTS2F400.14705 
C ******************************COPYRIGHT******************************    GTS2F400.14706 
C                                                                          GTS2F400.14707 
CLL  SUBROUTINE DD_ENV-------------------------------------------------    DDENV3A.3     
CLL                                                                        DDENV3A.4     
CLL  PURPOSE : CALCULATE THE EFFECT OF THE DOWNDRAUGHT                     DDENV3A.5     
CLL            ON THE LARGE_SCALE ATMOSPHERE                               DDENV3A.6     
CLL                                                                        DDENV3A.7     
CLL  SUITABLE FOR SINGLE COLUMN MODEL USE                                  DDENV3A.8     
CLL                                                                        DDENV3A.9     
CLL                                                                        DDENV3A.10    
CLL  MODEL            MODIFICATION HISTORY FROM MODEL VERSION 3.0:         DDENV3A.11    
CLL VERSION  DATE                                                          DDENV3A.12    
CLL   4.0   5/5/95    new deck added for vesrion 3A of convection          DDENV3A.13    
CLL                   scheme. Includes tracers and momentum in the         DDENV3A.14    
CLL                   downdraught.                                         DDENV3A.15    
CLL                   Pete Inness.                                         DDENV3A.16    
CLL                                                                        DDENV3A.17    
CLL  PROGRAMMING STANDARDS : UNIFIED MODEL DOCUMENTATION PAPER NO. 3       DDENV3A.18    
CLL  VERSION NO. 4  DATED 5/2/92                                           DDENV3A.19    
CLL                                                                        DDENV3A.20    
CLL  LOGICAL COMPONENTS COVERED:                                           DDENV3A.21    
CLL                                                                        DDENV3A.22    
CLL  SYSTEM TASK : P27                                                     DDENV3A.23    
CLL                                                                        DDENV3A.24    
CLL  DOCUMENTATION : UNIFIED MODEL DOCUMENTATION PAPER P27                 DDENV3A.25    
CLL                                                                        DDENV3A.26    
CLLEND-----------------------------------------------------------------    DDENV3A.27    
C                                                                          DDENV3A.28    
C*L  ARGUMENTS---------------------------------------------------------    DDENV3A.29    
C                                                                          DDENV3A.30    
      SUBROUTINE DD_ENV (NPNTS,NP_FULL,THDD_K,THDD_KM1,QDD_K,QDD_KM1,      DDENV3A.31    
     *                   THE_K,THE_KM1,QE_K,QE_KM1,DTHBYDT_K,              DDENV3A.32    
     *                   DTHBYDT_KM1,DQBYDT_K,DQBYDT_KM1,FLX_DD_K,         DDENV3A.33    
     *                   FLX_DD_KM1,DELPK,DELPKM1,DELTD,DELQD,AMDETK,      DDENV3A.34    
     *                   EKM14,B_DD_END,BDD_START,BDD_ON,L_MOM,UDD_K,      DDENV3A.35    
     *                   VDD_K,UDD_KM1,VDD_KM1,UE_K,VE_K,UE_KM1,VE_KM1,    DDENV3A.36    
     *                   DUBYDT_K,DUBYDT_KM1,DVBYDT_K,DVBYDT_KM1,DELUD,    DDENV3A.37    
     *                   DELVD,EFLUX_U_DD,EFLUX_V_DD,                      DDENV3A.38    
     *                   L_TRACER,NTRA,TRADD_K,TRADD_KM1,TRAE_K,           DDENV3A.39    
     *                   TRAE_KM1,DTRABYDT_K,DTRABYDT_KM1,DELTRAD)         DDENV3A.40    
C                                                                          DDENV3A.41    
      IMPLICIT NONE                                                        DDENV3A.42    
C                                                                          DDENV3A.43    
C-----------------------------------------------------------------------   DDENV3A.44    
C VECTOR LENGTHS AND LOOP COUNTERS                                         DDENV3A.45    
C-----------------------------------------------------------------------   DDENV3A.46    
C                                                                          DDENV3A.47    
      INTEGER NPNTS                 ! IN VECTOR LENGTH                     DDENV3A.48    
C                                                                          DDENV3A.49    
      INTEGER NP_FULL               ! IN FULL VECTOR LENGTH                DDENV3A.50    
C                                                                          DDENV3A.51    
      INTEGER I,KTRA                ! LOOP COUNTERS                        DDENV3A.52    
C                                                                          DDENV3A.53    
      INTEGER NTRA                  ! IN NUMBER OF TRACERS                 DDENV3A.54    
C                                                                          DDENV3A.55    
C-----------------------------------------------------------------------   DDENV3A.56    
C VARIABLES WHICH ARE INPUT                                                DDENV3A.57    
C-----------------------------------------------------------------------   DDENV3A.58    
C                                                                          DDENV3A.59    
      REAL THDD_K(NPNTS)            ! IN DOWNDRAUGHT POTENTIAL             DDENV3A.60    
                                    !    TEMPERATURE IN LAYER K (K)        DDENV3A.61    
C                                                                          DDENV3A.62    
      REAL THDD_KM1(NPNTS)          ! IN DOWNDRAUGHT POTENTIAL             DDENV3A.63    
                                    !    TEMPERATURE IN LAYER K-1 (K)      DDENV3A.64    
C                                                                          DDENV3A.65    
      REAL QDD_K(NPNTS)             ! IN DOWNDRAUGHT MIXING RATIO          DDENV3A.66    
                                    !    AT LAYER K (KG/KG)                DDENV3A.67    
C                                                                          DDENV3A.68    
      REAL QDD_KM1(NPNTS)           ! IN DOWNDRAUGHT MIXING RATIO          DDENV3A.69    
                                    !    AT LAYER K-1 (KG/KG)              DDENV3A.70    
C                                                                          DDENV3A.71    
      REAL UDD_K(NPNTS)             ! IN DOWNDRAUGHT U IN LAYER            DDENV3A.72    
                                    !    K (M/S)                           DDENV3A.73    
C                                                                          DDENV3A.74    
      REAL UDD_KM1(NPNTS)           ! IN DOWNDRAUGHT U IN LAYER            DDENV3A.75    
                                    !     K-1 (M/S)                        DDENV3A.76    
C                                                                          DDENV3A.77    
      REAL VDD_K(NPNTS)             ! IN DOWNDRAUGHT V IN LAYER            DDENV3A.78    
                                    !    K (M/S)                           DDENV3A.79    
C                                                                          DDENV3A.80    
      REAL VDD_KM1(NPNTS)           ! IN DOWNDRAUGHT V IN LAYER            DDENV3A.81    
                                    !     K-1 (M/S)                        DDENV3A.82    
C                                                                          DDENV3A.83    
      REAL TRADD_K(NP_FULL,NTRA)    ! IN DOWNDRAUGHT TRACER CONTENT        DDENV3A.84    
                                    !    IN LAYER K (KG/KG)                DDENV3A.85    
C                                                                          DDENV3A.86    
      REAL TRADD_KM1(NPNTS,NTRA)    ! IN DOWNDRAUGHT TRACER CONTENT        DDENV3A.87    
                                    !    AT LAYER K-1 (KG/KG)              DDENV3A.88    
C                                                                          DDENV3A.89    
      REAL THE_K(NPNTS)             ! IN POTENTIAL TEMPERATURE OF          DDENV3A.90    
                                    !    ENVIRONMENT IN LAYER K (K)        DDENV3A.91    
C                                                                          DDENV3A.92    
      REAL THE_KM1(NPNTS)           ! IN POTENTIAL TEMPERATURE OF          DDENV3A.93    
                                    !    ENVIRONMENT IN LAYER K-1 (K)      DDENV3A.94    
C                                                                          DDENV3A.95    
      REAL QE_K(NPNTS)              ! IN MIXING RATIO AT LAYER K (KG/KG)   DDENV3A.96    
C                                                                          DDENV3A.97    
      REAL QE_KM1(NPNTS)            ! IN MIXING RATIO AT LAYER K-1         DDENV3A.98    
                                    !    (KG/KG)                           DDENV3A.99    
C                                                                          DDENV3A.100   
      REAL UE_K(NPNTS)              ! IN U AT LAYER K (M/S)                DDENV3A.101   
C                                                                          DDENV3A.102   
      REAL UE_KM1(NPNTS)            ! IN U AT LAYER K-1 (M/S)              DDENV3A.103   
C                                                                          DDENV3A.104   
      REAL VE_K(NPNTS)              ! IN V AT LAYER K (M/S)                DDENV3A.105   
C                                                                          DDENV3A.106   
      REAL VE_KM1(NPNTS)            ! IN V AT LAYER K-1 (M/S)              DDENV3A.107   
C                                                                          DDENV3A.108   
      REAL TRAE_K(NP_FULL,NTRA)     ! IN TRACER CONTENT OF                 DDENV3A.109   
                                    !    ENVIRONMENT IN LAYER K (KG/KG)    DDENV3A.110   
C                                                                          DDENV3A.111   
      REAL TRAE_KM1(NP_FULL,NTRA)   ! IN TRACER CONTENT OF ENVIRONMENT     DDENV3A.112   
                                    !    IN LAYER K-1 (KG/KG)              DDENV3A.113   
C                                                                          DDENV3A.114   
      REAL FLX_DD_K(NPNTS)          ! IN MASS FLUX IN LAYER K (PA/S)       DDENV3A.115   
C                                                                          DDENV3A.116   
      REAL FLX_DD_KM1(NPNTS)        ! IN MASS FLUX IN LAYER K-1 (PA/S)     DDENV3A.117   
C                                                                          DDENV3A.118   
      REAL DELPK(NPNTS)             ! IN DIFFERENCE IN PRESSURE ACROSS     DDENV3A.119   
                                    !    LAYER K (PA)                      DDENV3A.120   
C                                                                          DDENV3A.121   
      REAL DELPKM1(NPNTS)           ! IN DIFFERENCE IN PRESSURE ACROSS     DDENV3A.122   
                                    !    LAYER K-1 (PA)                    DDENV3A.123   
C                                                                          DDENV3A.124   
      REAL DELTD(NPNTS)             ! IN COOLING NECESSARY TO ACHIEVE      DDENV3A.125   
                                    !    SATURATION (K)                    DDENV3A.126   
C                                                                          DDENV3A.127   
      REAL DELQD(NPNTS)             ! IN MOISTENING NECESSARY TO ACHIEVE   DDENV3A.128   
                                    !    SATURATION (KG/KG)                DDENV3A.129   
C                                                                          DDENV3A.130   
      REAL DELUD(NPNTS)             ! IN CHANGE TO ENVIRONMENT U DUE TO    DDENV3A.131   
                                    !    DOWNDRAUGHT FORMATION (M/S)       DDENV3A.132   
C                                                                          DDENV3A.133   
      REAL DELVD(NPNTS)             ! IN CHANGE TO ENVIRONMENT V DUE TO    DDENV3A.134   
                                    !    DOWNDRAUGHT FORMATION (M/S)       DDENV3A.135   
C                                                                          DDENV3A.136   
      REAL DELTRAD(NP_FULL,NTRA)    ! IN DEPLETION OF ENVIRONMENT TRACER   DDENV3A.137   
                                    !    DUE TO DOWNDRAUGHT FORMATION      DDENV3A.138   
C                                                                          DDENV3A.139   
      REAL AMDETK(NPNTS)            ! IN MIXING DETRAINMENT AT LEVEL K     DDENV3A.140   
                                    !    MULTIPLIED BY APPROPRIATE LAYER   DDENV3A.141   
                                    !    THICKNESS                         DDENV3A.142   
C                                                                          DDENV3A.143   
      REAL EKM14(NPNTS)             ! IN EXNER RATIO AT LAYER K-1/4        DDENV3A.144   
C                                                                          DDENV3A.145   
      LOGICAL B_DD_END(NPNTS)       ! IN MASK FOR THOSE POINTS WHERE       DDENV3A.146   
                                    !    DOWNDRAUGHT IS TERMINATING        DDENV3A.147   
C                                                                          DDENV3A.148   
      LOGICAL BDD_START(NPNTS)      ! IN MASK FOR THOSE POINTS WHERE       DDENV3A.149   
                                    !    DOWNDRAUGHT IS STARTING           DDENV3A.150   
C                                                                          DDENV3A.151   
      LOGICAL BDD_ON(NPNTS)         ! IN MASK FOR THOSE POINTS WHERE       DDENV3A.152   
                                    !    DOWNDRAUGHT IS ON                 DDENV3A.153   
C                                                                          DDENV3A.154   
      LOGICAL L_TRACER              ! IN SWITCH FOR INCLUSION OF TRACERS   DDENV3A.155   
C                                                                          DDENV3A.156   
      LOGICAL L_MOM                 ! IN SWITCH FOR INCLUSION OF           DDENV3A.157   
                                    !    MOMENTUM TRANSPORTS               DDENV3A.158   
C                                                                          DDENV3A.159   
C-----------------------------------------------------------------------   DDENV3A.160   
C VARIABLES WHICH ARE INPUT AND OUTPUT                                     DDENV3A.161   
C-----------------------------------------------------------------------   DDENV3A.162   
C                                                                          DDENV3A.163   
      REAL DTHBYDT_K(NPNTS)         ! INOUT                                DDENV3A.164   
                                    ! IN  INCREMENT TO POTENTIAL           DDENV3A.165   
                                    !     TEMPERATURE IN LAYER K (K/S)     DDENV3A.166   
                                    ! OUT UPDATED INCREMENT TO POTENTIAL   DDENV3A.167   
                                    !     TEMPERATURE IN LAYER K (K/S)     DDENV3A.168   
C                                                                          DDENV3A.169   
      REAL DTHBYDT_KM1(NPNTS)       ! INOUT                                DDENV3A.170   
                                    ! IN  INCREMENT TO POTENTIAL           DDENV3A.171   
                                    !     TEMPERATURE AT LAYER K-1 (K/S)   DDENV3A.172   
                                    ! OUT UPDATED INCREMENT TO POTENTIAL   DDENV3A.173   
                                    !     TEMPERATURE AT LAYER K-1 (K/S)   DDENV3A.174   
C                                                                          DDENV3A.175   
      REAL DQBYDT_K(NPNTS)          ! INOUT                                DDENV3A.176   
                                    ! IN  INCREMENT TO MIXING RATIO        DDENV3A.177   
                                    !     AT LAYER K (KG/KG/S)             DDENV3A.178   
                                    ! OUT UPDATED INCREMENT TO MIXING      DDENV3A.179   
                                    !     RATIO AT LAYER K (KG/KG/S)       DDENV3A.180   
C                                                                          DDENV3A.181   
      REAL DQBYDT_KM1(NPNTS)        ! INOUT                                DDENV3A.182   
                                    ! IN  INCREMENT TO MIXING RATIO AT     DDENV3A.183   
                                    !     LAYER K-1 (KG/KG/S)              DDENV3A.184   
                                    ! OUT UPDATED INCREMENT TO MIXING      DDENV3A.185   
                                    !     RATIO AT LAYER K-1 (KG/KG/S)     DDENV3A.186   
C                                                                          DDENV3A.187   
      REAL DUBYDT_K(NPNTS)          ! INOUT                                DDENV3A.188   
                                    ! IN  INCREMENT TO U AT LAYER K        DDENV3A.189   
                                    !     (M/S**2)                         DDENV3A.190   
                                    ! OUT UPDATED INCREMENT TO U           DDENV3A.191   
                                    !     AT LAYER K (M/S**2)              DDENV3A.192   
C                                                                          DDENV3A.193   
      REAL DUBYDT_KM1(NPNTS)        ! INOUT                                DDENV3A.194   
                                    ! IN  INCREMENT TO U AT LAYER K-1      DDENV3A.195   
                                    !     (M/S**2)                         DDENV3A.196   
                                    ! OUT UPDATED INCREMENT TO U           DDENV3A.197   
                                    !     AT LAYER K-1 (M/S**2)            DDENV3A.198   
C                                                                          DDENV3A.199   
      REAL DVBYDT_K(NPNTS)          ! INOUT                                DDENV3A.200   
                                    ! IN  INCREMENT TO V AT LAYER K        DDENV3A.201   
                                    !     (M/S**2)                         DDENV3A.202   
                                    ! OUT UPDATED INCREMENT TO V           DDENV3A.203   
                                    !     AT LAYER K (M/S**2)              DDENV3A.204   
C                                                                          DDENV3A.205   
      REAL DVBYDT_KM1(NPNTS)        ! INOUT                                DDENV3A.206   
                                    ! IN  INCREMENT TO V AT LAYER K-1      DDENV3A.207   
                                    !     (M/S**2)                         DDENV3A.208   
                                    ! OUT UPDATED INCREMENT TO V           DDENV3A.209   
                                    !     AT LAYER K-1 (M/S**2)            DDENV3A.210   
C                                                                          DDENV3A.211   
      REAL DTRABYDT_K(NP_FULL,NTRA) ! INOUT                                DDENV3A.212   
                                    ! IN  INCREMENT TO TRACER CONTENT      DDENV3A.213   
                                    !     IN LAYER K (KG/KG/S)             DDENV3A.214   
                                    ! OUT UPDATED INCREMENT TO TRACER      DDENV3A.215   
                                    !     CONTENT IN LAYER K (KG/KG/S)     DDENV3A.216   
C                                                                          DDENV3A.217   
      REAL DTRABYDT_KM1(NP_FULL,    ! INOUT                                DDENV3A.218   
     *                  NTRA)       ! IN  INCREMENT TO TRACER CONTENT      DDENV3A.219   
                                    !     AT LAYER K-1 (KG/KG/S)           DDENV3A.220   
                                    ! OUT UPDATED INCREMENT TO TRACER      DDENV3A.221   
                                    !     CONTENT AT LAYER K-1             DDENV3A.222   
                                    !     (KG/KG/S)                        DDENV3A.223   
C                                                                          DDENV3A.224   
      REAL EFLUX_U_DD(NPNTS),       ! INOUT                                DDENV3A.225   
     *     EFLUX_V_DD(NPNTS)        ! IN  EDDY FLUX OF MOMENTUM DUE TO     DDENV3A.226   
                                    !     DD AT TOP OF LAYER               DDENV3A.227   
                                    ! OUT EDDY FLUX OF MOMENTUM DUE TO     DDENV3A.228   
                                    !     DD AT BOTTOM OF LAYER            DDENV3A.229   
C                                                                          DDENV3A.230   
C-----------------------------------------------------------------------   DDENV3A.231   
C VARIABLES WHICH ARE DEFINED LOCALLY                                      DDENV3A.232   
C-----------------------------------------------------------------------   DDENV3A.233   
C                                                                          DDENV3A.234   
      REAL TEMPRY                   ! USED IN CALCULATIONS OF THE          DDENV3A.235   
                                    ! EFFECT ON THE ENVIRONMENT            DDENV3A.236   
C                                                                          DDENV3A.237   
      REAL FLX_U_KM0P5,             ! EDDY FLUX OF U AND V AT              DDENV3A.238   
     *     FLX_V_KM0P5              ! BOTTOM OF LAYER                      DDENV3A.239   
C                                                                          DDENV3A.240   
C-----------------------------------------------------------------------   DDENV3A.241   
C CALCULATE THE EFFECT ON THE ENVIRONMENT IN LAYER K                       DDENV3A.242   
C-----------------------------------------------------------------------   DDENV3A.243   
C                                                                          DDENV3A.244   
      DO I=1,NPNTS                                                         DDENV3A.245   
       IF (BDD_ON(I)) THEN                                                 DDENV3A.246   
C                                                                          DDENV3A.247   
C-----------------------------------------------------------------------   DDENV3A.248   
C SUBTRACT ENERGY USED TO FORM DOWNDRAUGHT                                 DDENV3A.249   
C-----------------------------------------------------------------------   DDENV3A.250   
C                                                                          DDENV3A.251   
       TEMPRY = FLX_DD_K(I)/DELPK(I)                                       DDENV3A.252   
       IF (BDD_START(I)) THEN                                              DDENV3A.253   
         DTHBYDT_K(I) = DTHBYDT_K(I)-TEMPRY*DELTD(I)                       DDENV3A.254   
         DQBYDT_K(I) = DQBYDT_K(I)-TEMPRY*DELQD(I)                         DDENV3A.255   
       END IF                                                              DDENV3A.256   
C                                                                          DDENV3A.257   
C-----------------------------------------------------------------------   DDENV3A.258   
C EFFECT OF CONVECTION AND DOWNDRAUGHT UPON POTENTIAL TEMPERATURE OF       DDENV3A.259   
C LAYER K                                                                  DDENV3A.260   
C-----------------------------------------------------------------------   DDENV3A.261   
C                                                                          DDENV3A.262   
       DTHBYDT_K(I) = DTHBYDT_K(I) + TEMPRY * (                            DDENV3A.263   
     *                                                                     DDENV3A.264   
     *          (1.0+EKM14(I)) * (1.0-AMDETK(I)) *      ! COMPENSATING     DDENV3A.265   
     *           (THE_KM1(I)-THE_K(I))                  ! SUBSIDENCE       DDENV3A.266   
     *        +                                                            DDENV3A.267   
     *          AMDETK(I)* (THDD_K(I)-THE_K(I))         ! MIXING           DDENV3A.268   
     *        )                                         ! DETRAINMENT      DDENV3A.269   
C                                                                          DDENV3A.270   
C-----------------------------------------------------------------------   DDENV3A.271   
C EFFECT OF CONVECTION AND DOWNDRAUGHT UPON MIXING RATIO OF                DDENV3A.272   
C LAYER K                                                                  DDENV3A.273   
C-----------------------------------------------------------------------   DDENV3A.274   
C                                                                          DDENV3A.275   
       DQBYDT_K(I) = DQBYDT_K(I) + TEMPRY * (                              DDENV3A.276   
     *                                                                     DDENV3A.277   
     *      (1.0+EKM14(I)) * (1.0-AMDETK(I)) *       ! COMPENSATING        DDENV3A.278   
     *      (QE_KM1(I)-QE_K(I))                      ! SUBSIDENCE          DDENV3A.279   
     *    +                                                                DDENV3A.280   
     *      AMDETK(I)* (QDD_K(I)-QE_K(I))            ! MIXING              DDENV3A.281   
     *    )                                          ! DETRAINMENT         DDENV3A.282   
C                                                                          DDENV3A.283   
C                                                                          DDENV3A.284   
C-----------------------------------------------------------------------   DDENV3A.285   
C TERMINAL DETRAINMENT AND SUBSIDENCE IN TERMINAL LAYER                    DDENV3A.286   
C-----------------------------------------------------------------------   DDENV3A.287   
C                                                                          DDENV3A.288   
         IF (B_DD_END(I)) THEN                                             DDENV3A.289   
           TEMPRY = FLX_DD_KM1(I)/DELPKM1(I)                               DDENV3A.290   
           DTHBYDT_KM1(I) = DTHBYDT_KM1(I)+TEMPRY*                         DDENV3A.291   
     *                      (THDD_KM1(I)-THE_KM1(I))                       DDENV3A.292   
           DQBYDT_KM1(I) = DQBYDT_KM1(I)+TEMPRY*(QDD_KM1(I)-QE_KM1(I))     DDENV3A.293   
C                                                                          DDENV3A.294   
         END IF                                                            DDENV3A.295   
C                                                                          DDENV3A.296   
       END IF                                                              DDENV3A.297   
      END DO                                                               DDENV3A.298   
C                                                                          DDENV3A.299   
C-----------------------------------------------------------------------   DDENV3A.300   
C EFFECT OF CONVECTION AND DOWNDRAUGHT UPON MOMENTUM OF                    DDENV3A.301   
C LAYER K                                                                  DDENV3A.302   
C-----------------------------------------------------------------------   DDENV3A.303   
C                                                                          DDENV3A.304   
      IF(L_MOM)THEN                                                        DDENV3A.305   
C                                                                          DDENV3A.306   
      DO I=1,NPNTS                                                         DDENV3A.307   
      IF(BDD_ON(I))THEN                                                    DDENV3A.308   
       TEMPRY = FLX_DD_K(I)/DELPK(I)                                       DDENV3A.309   
       IF(BDD_START(I))THEN                                                DDENV3A.310   
         DUBYDT_K(I) = DUBYDT_K(I)-TEMPRY*DELUD(I)                         DDENV3A.311   
         DVBYDT_K(I) = DVBYDT_K(I)-TEMPRY*DELVD(I)                         DDENV3A.312   
       END IF                                                              DDENV3A.313   
C                                                                          DDENV3A.314   
C----------------------------------------------------------------------    DDENV3A.315   
C CALCULATE MOMENTUM FLUX AT BOTTOM OF LAYER                               DDENV3A.316   
C----------------------------------------------------------------------    DDENV3A.317   
C                                                                          DDENV3A.318   
       FLX_U_KM0P5 = FLX_DD_K(I) * (1.0-AMDETK(I)) * (1.0+EKM14(I)) *      DDENV3A.319   
     *               (UDD_K(I) - UE_KM1(I))                                DDENV3A.320   
       FLX_V_KM0P5 = FLX_DD_K(I) * (1.0-AMDETK(I)) * (1.0+EKM14(I)) *      DDENV3A.321   
     *               (VDD_K(I) - VE_KM1(I))                                DDENV3A.322   
C                                                                          DDENV3A.323   
C---------------------------------------------------------------------     DDENV3A.324   
C CALCULATE INCREMENT TO LAYER K                                           DDENV3A.325   
C---------------------------------------------------------------------     DDENV3A.326   
C                                                                          DDENV3A.327   
       IF (BDD_START(I)) THEN                                              DDENV3A.328   
C                                                                          DDENV3A.329   
        DUBYDT_K(I) = DUBYDT_K(I) - FLX_U_KM0P5/DELPK(I)                   DDENV3A.330   
        DVBYDT_K(I) = DVBYDT_K(I) - FLX_V_KM0P5/DELPK(I)                   DDENV3A.331   
C                                                                          DDENV3A.332   
C STORE FLUX READY FOR UPDATING NEXT LAYER                                 DDENV3A.333   
C                                                                          DDENV3A.334   
        EFLUX_U_DD(I) = FLX_U_KM0P5                                        DDENV3A.335   
        EFLUX_V_DD(I) = FLX_V_KM0P5                                        DDENV3A.336   
C                                                                          DDENV3A.337   
       ELSE                                                                DDENV3A.338   
C                                                                          DDENV3A.339   
        DUBYDT_K(I) = DUBYDT_K(I) + ((EFLUX_U_DD(I) - FLX_U_KM0P5)/        DDENV3A.340   
     *                                                    DELPK(I))        DDENV3A.341   
        DVBYDT_K(I) = DVBYDT_K(I) + ((EFLUX_V_DD(I) - FLX_V_KM0P5)/        DDENV3A.342   
     *                                                    DELPK(I))        DDENV3A.343   
C                                                                          DDENV3A.344   
C STORE FLUX READY FOR UPDATING NEXT LAYER                                 DDENV3A.345   
C                                                                          DDENV3A.346   
        EFLUX_U_DD(I) = FLX_U_KM0P5                                        DDENV3A.347   
        EFLUX_V_DD(I) = FLX_V_KM0P5                                        DDENV3A.348   
C                                                                          DDENV3A.349   
       END IF                                                              DDENV3A.350   
C                                                                          DDENV3A.351   
CL--------------------------------------------------------------------     DDENV3A.352   
CL TERMINAL DETRAINMENT OF MOMENTUM                                        DDENV3A.353   
CL-------------------------------------------------------------------      DDENV3A.354   
C                                                                          DDENV3A.355   
        IF(B_DD_END(I))THEN                                                DDENV3A.356   
           DUBYDT_KM1(I) = FLX_U_KM0P5/DELPKM1(I)                          DDENV3A.357   
           DVBYDT_KM1(I) = FLX_V_KM0P5/DELPKM1(I)                          DDENV3A.358   
C                                                                          DDENV3A.359   
C ZERO FLUX AT BOTTOM OF LAYER                                             DDENV3A.360   
C                                                                          DDENV3A.361   
           EFLUX_U_DD(I) = 0.0                                             DDENV3A.362   
           EFLUX_V_DD(I) = 0.0                                             DDENV3A.363   
        END IF                                                             DDENV3A.364   
      END IF                                                               DDENV3A.365   
      END DO                                                               DDENV3A.366   
C                                                                          DDENV3A.367   
      END IF                                                               DDENV3A.368   
C                                                                          DDENV3A.369   
C-----------------------------------------------------------------------   DDENV3A.370   
C EFFECT OF CONVECTION AND DOWNDRAUGHT UPON TRACER CONTENT OF              DDENV3A.371   
C LAYER K                                                                  DDENV3A.372   
C-----------------------------------------------------------------------   DDENV3A.373   
C                                                                          DDENV3A.374   
      IF(L_TRACER)THEN                                                     DDENV3A.375   
C                                                                          DDENV3A.376   
      DO KTRA=1,NTRA                                                       DDENV3A.377   
       DO I=1,NPNTS                                                        DDENV3A.378   
        IF(BDD_ON(I))THEN                                                  DDENV3A.379   
C                                                                          DDENV3A.380   
        TEMPRY = FLX_DD_K(I)/DELPK(I)                                      DDENV3A.381   
        IF(BDD_START(I))THEN                                               DDENV3A.382   
        DTRABYDT_K(I,KTRA) = DTRABYDT_K(I,KTRA)-TEMPRY*DELTRAD(I,KTRA)     DDENV3A.383   
        END IF                                                             DDENV3A.384   
        DTRABYDT_K(I,KTRA) = DTRABYDT_K(I,KTRA) + TEMPRY * (               DDENV3A.385   
     *                                                                     DDENV3A.386   
     *   (1.0+EKM14(I)) * (1.0-AMDETK(I)) *             ! COMPENSATING     DDENV3A.387   
     *   (TRAE_KM1(I,KTRA)-TRAE_K(I,KTRA))              ! SUBSIDENCE       DDENV3A.388   
     *        +                                                            DDENV3A.389   
     *   AMDETK(I)* (TRADD_K(I,KTRA)-TRAE_K(I,KTRA))    ! MIXING           DDENV3A.390   
     *        )                                         ! DETRAINMENT      DDENV3A.391   
C                                                                          DDENV3A.392   
C-----------------------------------------------------------------------   DDENV3A.393   
C TERMINAL DETRAINMENT OF TRACER                                           DDENV3A.394   
C-----------------------------------------------------------------------   DDENV3A.395   
C                                                                          DDENV3A.396   
           IF(B_DD_END(I))THEN                                             DDENV3A.397   
             TEMPRY = FLX_DD_KM1(I)/DELPKM1(I)                             DDENV3A.398   
             DTRABYDT_KM1(I,KTRA)=DTRABYDT_KM1(I,KTRA)+TEMPRY*             DDENV3A.399   
     *                            (TRADD_KM1(I,KTRA)-TRAE_KM1(I,KTRA))     DDENV3A.400   
           END IF                                                          DDENV3A.401   
C                                                                          DDENV3A.402   
        END IF                                                             DDENV3A.403   
       END DO                                                              DDENV3A.404   
      END DO                                                               DDENV3A.405   
C                                                                          DDENV3A.406   
      END IF                                                               DDENV3A.407   
C                                                                          DDENV3A.408   
      RETURN                                                               DDENV3A.409   
      END                                                                  DDENV3A.410   
C                                                                          DDENV3A.411   
*ENDIF                                                                     DDENV3A.412   
